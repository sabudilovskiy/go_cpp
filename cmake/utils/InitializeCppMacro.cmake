function(InitializeCppMacro)
    set(oneValueArgs target name_macro variable default)
    set(multiValueArgs "")
    set(options)
    cmake_parse_arguments(ARG "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})
    if (NOT DEFINED ARG_target OR NOT DEFINED ARG_name_macro OR NOT DEFINED ARG_variable)
        message(FATAL_ERROR "Не передан target, name_macro или variable")
    endif ()
    if (DEFINED ${PROJECT_NAME}_${ARG_variable})
        set (value_variable ${${PROJECT_NAME}_${ARG_variable}})
    elseif (DEFINED ARG_default)
        set (value_variable ${ARG_default})
    else ()
        message(WARNING "Не задана ни переменная: " ${PROJECT_NAME}_${ARG_variable}", ни дефолтное значение. Таргет:  ${ARG_target}, макрос: ${ARG_name_macro}")
    endif ()

    if (DEFINED value_variable)
        get_target_property(target_type ${ARG_target} TYPE)
        if (${target_type} STREQUAL "INTERFACE_LIBRARY")
            set(type_visible INTERFACE)
        else ()
            set(type_visible PUBLIC)
        endif ()
        target_compile_definitions(${ARG_target} ${type_visible} -D${ARG_name_macro}=${value_variable})
    endif ()
endfunction()
